apply plugin: 'groovy'
apply plugin: 'antlr'
apply plugin: 'application'

// configure application
mainClassName = 'com.github.oreissig.hrm.Main'
if (project.hasProperty('args'))
    run.args = args.split(' ') as List

// configure code coverage
apply plugin: 'jacoco'
jacoco.toolVersion = '0.7.5.+'
tasks.withType(Test) { it.finalizedBy 'jacocoTestReport' }

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.5.+'
    compile localGroovy()
    compile 'org.antlr:antlr4-runtime:4.5.+'
    compile 'org.slf4j:slf4j-api:1.7.12'
    runtime 'commons-cli:commons-cli:1.3.1'
    
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'org.slf4j:slf4j-simple:1.7.12'
    // cglib and objenesis needed for advanced mocking
    testRuntime 'cglib:cglib:2.2.2'
    testRuntime 'org.objenesis:objenesis:2.2'
}

configurations.all {
    // use localGroovy()
    exclude module: 'groovy-all'
}