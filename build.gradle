group = 'com.github.oreissig'
version = '1.0.0'

apply plugin: 'groovy'
apply plugin: 'antlr'
apply plugin: 'application'
apply from: 'bintray-publish.gradle'

// use invokedynamic for groovy because it's so cool
tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

// configure application
mainClassName = 'com.github.oreissig.hrm.launcher.Main'
if (project.hasProperty('args'))
    run.args = args.split(' ') as List
run.standardInput = System.in

// configure code coverage
apply plugin: 'jacoco'
jacoco.toolVersion = '0.7.6.+'
tasks.withType(Test) { it.finalizedBy 'jacocoTestReport' }

repositories {
    jcenter()
}

dependencies {
    antlr   'org.antlr:antlr4:4.5.2-1'
    compile 'org.antlr:antlr4-runtime:4.5.2-1'
    compile 'org.codehaus.groovy:groovy-all:2.4.6:indy'
    runtime 'commons-cli:commons-cli:1.3.1'
    
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    // cglib and objenesis needed for advanced mocking
    testRuntime 'cglib:cglib:3.2.1'
    testRuntime 'org.objenesis:objenesis:2.2'
}

tasks.distTar {
    compression = 'GZIP'
}
